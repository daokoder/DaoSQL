#load mysql;
load sqlite;

# class that can be mapped to a database table
class Gene
{
	var id   : SQL::INTEGER_PRIMARY_KEY_AUTO_INCREMENT;
	var name : SQL::VARCHAR128;
	var seq  : SQL::TEXT;
}
# connect database
#db = SQLDatabase<PostgreSQL>( "dbname", "host", "user", "pwd" );
#db = SQLDatabase<MySQL>( "dbname", "host", "user", "pwd" );
var db = SQL::Database<SQL::SQLite>( "Test_Gene.db" );

# create a table for class Gene
std.exec {
	defer(any){ return none }
	db.CreateTable( Gene );
}

var gene = Gene.{ 0, "RAS", "...AATCCGG..." };
var gene2 = Gene.{ 0, "RAS2", "...AATCCGCCC..." };
# insert a record into the table
db.Insert( gene, gene2 );

# SELECT * FROM Gene WHERE name = "RASE";
var h = db.Select( Gene ).Where().EQ( "name", "RAS2" );
io.writeln( std.about(h) )

io.writeln( gene.id, gene.name, gene.seq );
# query and store the result into "gene" object:
h.Query( gene ){
	io.writeln( gene.id, gene.name, gene.seq );
	return $continue;
}

# TODO: Add such database method;
routine Transaction()[]
{
	defer(any){
		io.writeln( "Rolling Back!" )
		db.RollBack()
	}
	db.Begin();
	yield()
	db.Commit();
}

Transaction {
	var h = db.Select( Gene ).Where().EQ( "name2", "RAS2" );

	io.writeln( gene.id, gene.name, gene.seq );
	if( h.QueryOnce( gene ) ){
		io.writeln( gene.id, gene.name, gene.seq );
	}
}
